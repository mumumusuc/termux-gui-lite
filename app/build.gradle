plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "com.google.protobuf"
}

protobuf {
    protoc {
        path = "protoc"
    }
    generateProtoTasks {
        all().forEach {
            it.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

android {
    compileSdk 33
    defaultConfig {
        applicationId "com.termux.gui"
        minSdk 24
        targetSdk 33
        versionCode 8
        versionName "1.0.0"
        externalNativeBuild {
            cmake {
                arguments ''
                abiFilters "arm64-v8a"
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile file('dev_keystore.jks')
            keyAlias 'alias'
            storePassword 'xrj45yWGLbsO7W0v'
            keyPassword 'xrj45yWGLbsO7W0v'
        }
    }
    packagingOptions {
        jniLibs {
            keepDebugSymbols += ['**/*.so']
        }
    }


    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
        }
        debugMinify {
            debuggable true
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
    ndkVersion '26.1.10909125'
    namespace 'com.termux.gui'
}

dependencies {
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.activity:activity-ktx:1.5.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    implementation "androidx.security:security-crypto:1.1.0-alpha03"
    
    implementation "com.google.protobuf:protobuf-javalite:3.19.4"
    
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation project(path: ':hbuffers')
    
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
